buildscript {

    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'spoon'

repositories {

    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

task printProps << {

    println 'Printing properties'
    println android_keystore
    println android_keystore_password
    println android_key_alias
    println android_key_password

}

dependencies {

    // App's dependencies, including test
//    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':android-proxy-library')

    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'

    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'com.google.code.gson:gson:2.3'

    compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
        transitive = true
    }

    compile 'com.jakewharton.timber:timber:2.7.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.afollestad:material-dialogs:0.6.2.1'
    compile 'com.nispok:snackbar:2.8.0'
    compile 'com.jpardogo.materialtabstrip:library:1.0.8'
    compile 'com.github.traex.rippleeffect:library:1.2.3'
    compile 'com.getbase:floatingactionbutton:1.8.0'
    compile 'info.hoang8f:fbutton:1.0.5'
    compile 'com.makeramen:roundedimageview:1.5.0'
    compile 'de.hdodenhof:circleimageview:1.2.1'
//    compile 'com.rengwuxian.materialedittext:library:1.8.2'   // Not stable at the moment

    // Testing dependencies
    compile 'com.android.support:support-annotations:21.0.3'    // Required by Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

}

allprojects {
    // show deprecation warnings for all projects
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:unchecked"
    }
}

android {

    compileSdkVersion 21
    buildToolsVersion '21.1.2'

//    Scheme for version codes: ##@@$$$%% , using:
//    ##   - the first two digits for the API Level,
//    @@   - the second and third digits for the minimum and maximum screen size
//           (1 - 4 indicating each of the four sizes) or to denote the texture formats
//
///          the last five digits are for the app version:
//    $$$  - three digits for major version (the user will see only the major version)
///   %%   - two digits for minor version

    defaultConfig {

        minSdkVersion 15
        targetSdkVersion 21
        versionCode 130030001
        versionName "3.00"

        applicationId "com.lechucksoftware.proxy.proxysettings"

        testApplicationId "com.lechucksoftware.proxy.proxysettings.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        testHandleProfiling true
        testFunctionalTest true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
    }

    signingConfigs {
        release
    }

    sourceSets {

        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']

//            manifest.srcFile 'AndroidManifest.xml'
//            java.srcDirs = ['src/main/java']
//            resources.srcDirs = ['src/main/src']
//            aidl.srcDirs = ['src/main/src']
//            renderscript.srcDirs = ['src/main/src']
//            res.srcDirs = ['src/main/res']
//            assets.srcDirs = ['src/main/assets']
        }

//        androidTest.setRoot('src/androidTest')

        androidTest {
            java.srcDirs = ['src/androidTest/java']
        }
    }

    productFlavors {

        play {
            buildConfigField "String", "MARKET_URI", "\"com.android.vending\""
        }

        amazon {
            buildConfigField "String", "MARKET_URI", "\"com.amazon.venezia\""
        }
    }

    buildTypes {

        release {

            // As long as proxy settings is open source this shouldn't be needed
            minifyEnabled false
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            debuggable false

            buildConfigField "String", "ANALYTICS_TRACK_ID", "\"" + (project.hasProperty('analytics_track_id_release') ? analytics_track_id_release : 1234)  + "\""

            signingConfig project.hasProperty('android_keystore') ? signingConfigs.release : signingConfigs.debug

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionCode + ".apk"))
                }
            }
        }

        debug {

            minifyEnabled false
            shrinkResources false

            debuggable true

            versionNameSuffix "-DEBUG"

            buildConfigField "String", "ANALYTICS_TRACK_ID", "\"" + (project.hasProperty('analytics_track_id_debug') ? analytics_track_id_debug : 1234)  + "\""
        }

        CI {

            debuggable true
            shrinkResources false

            minifyEnabled false

            versionNameSuffix "-CI"

            buildConfigField "String", "ANALYTICS_TRACK_ID", 'null'
            ext.enableCrashlytics = false
        }
    }

    if (project.hasProperty('android_keystore') &&
            project.hasProperty('android_keystore_password') &&
            project.hasProperty('android_key_alias') &&
            project.hasProperty('android_key_password')) {

        println 'Signing Configurations succesfully found'

        android.signingConfigs.release.storeFile = file(android_keystore)
        android.signingConfigs.release.storePassword = android_keystore_password
        android.signingConfigs.release.keyAlias = android_key_alias
        android.signingConfigs.release.keyPassword = android_key_password

    } else {

        println 'Signing Configurations not found'
        buildTypes.release.signingConfig = null
    }
}

